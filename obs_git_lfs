#!/usr/bin/perl -w
#
# Copyright (c) 2022 Adrian Schroeter, SUSE LLC
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License version 2 as
# published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program (see the file COPYING); if not, write to the
# Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA
#
################################################################
#
# OBS Git LFS provider
# 
# This service answers git LFS requests and let OBS deliver the files
# based on a static database.
#
# This is only supposed to be used for converted sources from classic
# OBS source repositories. This is not supposed to be used for sources
# which are supposed to be actively maintained in git directly.
#

BEGIN {
  my ($wd) = $0 =~ m-(.*)/- ;
  $wd ||= '.';
  unshift @INC,  "$wd";
}

use Data::Dumper;
use JSON::XS ();

use BSServer;
use BSDispatch;
use BSSQLite;

use strict;

my $port = 9999;
my $proto = 'http';

my $db_file = "/etc/obs-git-lfs.db";

sub getgitlfs {
  die("need batch data as content\n") unless BSServer::have_content();
  my $batch_json = BSServer::read_data(10000000);
  my $batch = JSON::XS::decode_json($batch_json);

  # print $batch_json;
  # print Dumper($batch);

  die("not a download operation\n") unless $batch->{'operation'} eq 'download';
  die("not using sha256 as hash algo\n") unless $batch->{'hash_algo'} eq 'sha256';

  my $h = BSSQLite::connectdb($db_file);
  my @resobj;
  for my $obj (@{$batch->{'objects'}}) {
    my $oid = $obj->{'oid'};
    my $size = $obj->{'size'};
    my @ary = BSSQLite::selectrow($h, "SELECT project,package,filename,rev FROM lfs_oids WHERE sha256 = ? AND size = ?", $oid, $size);
    #  print Dumper(@ary);
    if (@ary != 4) {
      print "ERROR: $oid/$size not found in database\n";
      push @resobj, {
        oid => $oid,
        size => int($size),
        error => {
          code => 404,
          message => "Object does not exist",
        },
      };
      next;
    };
    my ($projid, $packid, $filename, $md5) = @ary;
    my $url = BSHTTP::urlencode("https://api.opensuse.org/public/source/$projid/$packid/$filename")."?rev=$md5";
    print "$oid $size: $url\n";
    push @resobj, {
      oid => $oid,
      size => int($size),
      authenticated => JSON::XS::false,
      actions => {
	download => { href => $url, },
      },
    };
  }
  my $resp = {
    transfer => 'basic',
    hash_algo => 'sha256',
    objects => \@resobj,
  };
  #  print Dumper(JSON::XS::encode_json($resp));
  return (JSON::XS::encode_json($resp), 'Content-Type: application/json');
}

sub dummylock {
  die('404 This is a read-only server');
}

# define server
my $dispatches = [
  '/' => sub {("{\"name\": \"OBS git LFS Server\"}\n", 'Content-Type: application/json')},
  'POST:/gitlfs/objects/batch' => \&getgitlfs,
  'POST:/gitlfs/locks/verify' => \&dummylock,
];

my $conf = {
  'port' => $port,
  'proto' => $proto,
  'dispatch' => \&BSDispatch::dispatch,
  'dispatches' => $dispatches,
  'setkeepalive' => 1,
  'maxchild' => 20,
};

BSDispatch::compile($conf);
BSServer::serveropen($port);
BSServer::server($conf);
